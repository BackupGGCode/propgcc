MEMORY {
    hub (wxa) : ORIGIN = 0x00000000, LENGTH = 32K
    rom (rx) : ORIGIN = 0x30000000, LENGTH = 1M
    ram (wrx) : ORIGIN = 0x20000000, LENGTH = 64K
    cog (wxa) : ORIGIN = 0x00000000, LENGTH = 2K
}

SECTIONS {
    .xmmkernel : {
        *(.xmmkernel)
    } >cog
    
    .header : {
        LONG(entry)
        LONG(ADDR(.bss))
        LONG(ADDR(.bss) + SIZEOF(.bss))
        LONG(LOADADDR(.data))
        LONG(ADDR(.data))
        LONG(ADDR(.data) + SIZEOF(.data) + SIZEOF(.hubtext) + SIZEOF(.ctors) + SIZEOF(.dtors))
    } >ram
    .init : {
	    *(.init*)
    } >ram
    .text : {
        *(.text*)
    } >ram
    
    .data : {
        *(.hubstart)
        *(.data*)
    } >hub AT>ram
    .hubtext : {
        *(.hubtext)
    } >hub AT>ram
    .ctors : {
        *(.ctors*)
    } >hub AT>ram
    .dtors : {
        *(.dtors*)
    } >hub AT>ram
    .bss : {
        *(.bss*)
    } >hub AT>hub
    
  ___heap_start = ALIGN(4);

  ___CTOR_LIST__ = ADDR(.ctors) ;
  ___DTOR_LIST__ = ADDR(.dtors) ;
}
